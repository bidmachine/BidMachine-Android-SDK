apply plugin: 'com.android.application'
apply from: "../config.gradle"

android {
    compileSdkVersion androidCompileSdkVersion

    defaultConfig {
        applicationId "io.bidmachine.test.app"
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode 100100029
        versionName "1.1.5"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    buildTypes {
        release {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'bidmachine'
    productFlavors {
        local {
            dimension 'bidmachine'
        }
        remote {
            dimension 'bidmachine'
        }
        localRemote {
            dimension 'bidmachine'
        }
    }
}

repositories {
    maven {
        url 'https://artifactory.bidmachine.io/bidmachine'
    }
    maven {
        url 'https://artifactory.bidmachine.io/bidmachine-private'
        credentials {
            username = System.getenv("APD_USERNAME")
            password = System.getenv("APD_USERPASSWORD")
        }
    }
    mavenLocal()
    mavenCentral()
}

dependencies {
    //for play service v15+
    implementation('com.google.android.gms:play-services-ads-identifier:15.0.0') {
        exclude module: 'support-v4'
        exclude module: 'support-v7'
    }

    //for play service < 15
    //    implementation("com.google.android.gms:play-services-basement:15.0.0") {
    //        exclude module: 'support-v4'
    //        exclude module: 'support-v7'
    //    }

    //for play service < 8.3.0
    //    implementation("com.google.android.gms:play-services-ads:7.3.0") {
    //        exclude module: 'support-v4'
    //        exclude module: 'support-v7'
    //    }

    implementation "androidx.multidex:multidex:2.0.1"
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.1"
    implementation "org.jsoup:jsoup:1.11.3"
    implementation "com.google.android.material:material:1.2.0"
    implementation "com.wefika:flowlayout:0.4.1"
    implementation "com.google.code.gson:gson:2.8.5"

    testImplementation "junit:junit:4.12"

    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.1.0"

    localImplementation project(':bidmachine')

    remoteImplementation "io.bidmachine:ads:${project(':bidmachine').android.defaultConfig.versionName}"
    localRemoteImplementation "io.bidmachine:ads:${project(':bidmachine').android.defaultConfig.versionName}"
}

gradle.projectsEvaluated {
    dependencies {
        collectAdapters().forEach { adapterProject ->
            println("register dependency: ${adapterProject.name}")
            localImplementation(adapterProject)
            remoteImplementation "io.bidmachine:ads.networks.${adapterProject.name}:${adapterProject.minBidMachineSdkVersion}.${adapterProject.android.defaultConfig.versionCode}"
        }
    }
}
